plugins  {
    id 'org.smartbit4all.platform.gradle.java' version '0.3.8' apply false
	id "org.sonarqube" version "3.0"
}

sonarqube {
  properties {
    property "sonar.projectKey", "smartbit4all_platform"
    property "sonar.organization", "smartbit4all"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.exclusions", "**/src-gen/**"
  }
}

subprojects {
	apply plugin: 'org.smartbit4all.platform.gradle.java'
	    apply plugin: 'maven-publish'
	    group "org.smartbit4all.platform"
	    version "1.0.3-SNAPSHOT"
	
	    sourceCompatibility = JavaVersion.VERSION_1_8
	    targetCompatibility = JavaVersion.VERSION_1_8
	
	    dependencies {
	        api 'com.google.guava:guava:31.0.1-jre'
	    }
	
		java {
			withSourcesJar()
		}
	
		sourcesJar {
			duplicatesStrategy = 'exclude'
		}

	    publishing {
	        publications {
	            maven(MavenPublication) {
	                from components.java
	            }
	        }
	
		    repositories {
				maven {
		            name = "GitHubPackages"
		            url = uri("https://maven.pkg.github.com/it4all-hungary/p001")
		            credentials {
		                username = project.findProperty("gpr.user") ?: System.getenv("GitHub_Username_it4all")
		                password = project.findProperty("gpr.key") ?: System.getenv("GitHub_Token_it4all_package")
		            }
	        	}
		    }
	    }

}

task clean {
	subprojects.each { dependsOn("${it.name}:clean") }
}

task jar {
	subprojects.each { dependsOn("${it.name}:jar") }
}

task sourcesJar {
	subprojects.each { dependsOn("${it.name}:sourcesJar") }
}

task publish {
	dependsOn 'jar'
	dependsOn 'sourcesJar'
	subprojects.each { dependsOn("${it.name}:publish") }
}

task publishToMavenLocal {
	dependsOn 'jar'
	dependsOn 'sourcesJar'
	subprojects.each { dependsOn("${it.name}:publishToMavenLocal") }
}

